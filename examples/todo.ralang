app Todo {
	title: "Todo App Created by Ra",
	auth: {
		userModel: User,
		idField: id,
		isOnlineField: isOnline,
		lastActiveField: lastActive,
		usernameField: username,
		passwordField: password,
		onSuccessRedirectTo: "/",
		onFailRedirectTo: "/login"
	}
}

model User {
	id        Int     @id
	username  String  @unique
	password  String
	isOnline	 Boolean	 @default(false)
	lastActive DateTime  @default(Now)
	tasks 		Task[]
}

model Task {
	id        Int      @id
	title     String
	isDone    Boolean  @default(false)
	user 			User 		 @relation(userId, id)
	userId		Int
	createdAt DateTime @default(Now)
	updatedAt DateTime @updatedAt
}

@model(Task)
@permissions(IsAuth, OwnsEntry)
query<FindMany> getTasks {
	search: [ title, isDone ]
}

@model(Task)
@permissions(IsAuth, OwnsEntry)
query<FindUnique> getTaskById {
	where: [ id ]
}

@model(Task)
@permissions(IsAuth)
query<Create> createTask {
	data: {
		fields: [title],
		relationFields: {
			user: connect id with userId
		}
	}
}

@model(Task)
@permissions(IsAuth, OwnsEntry)
query<Update> updateTaskById {
	where: [ id ],
	data: {
		fields: [ title, isDone ]
	}
}

@model(Task)
@permissions(IsAuth, OwnsEntry)
query<Delete> deleteTaskById {
	where: [ id ]
}

component<Create> TaskCreateForm {
	actionQuery: createTask,
	globalStyle: {
		form: "flex mt-4"
	},
	formInputs: {
		title: {
			style: "flex w-full",
			input: {
				type: TextInput,
				placeholder: "Enter task title",
				isVisible: true,
				style: "shadow border rounded py-2 px-3 w-full mr-4 text-grey-darker"
			}
		},
		user: {
			input: {
				type: RelationInput,
				isVisible: false,
				defaultValue: connect id with LoggedInUser.id
			}
		}
	},
	formButton: {
		name: "Create",
		style: "rounded-md bg-teal-500 text-white px-4 py-2"
	}
}

component<Delete> TaskDeleteButton(id: Int) {
	actionQuery: deleteTaskById,
	formButton: {
		name: "Delete",
		style: "rounded-md bg-red-500 text-white px-4 py-2"
	}
}

component TaskComponent(task: Task) {
	render(
		<div className="flex mb-4 items-center">
			<div className="w-full text-gray-500 text-xl">{ task.title }</div>
			<TaskDeleteButton id={task.id} />
		</div>
	)
}

component<FindMany> TasksComponent {
	findQuery: getTasks as tasks,
	onError: render(
		<div>{ "An error occured" }</div>
	),
	onLoading: render(
		<div>{ "Loading..." }</div>
	),
	onSuccess: render(
		<>
				[% for task in tasks %]
					<div>
						<TaskComponent task={ task } />
					</div>
				[% endfor %]
		</>
	)
}

component<SignupForm> MySignupForm {
	globalStyle: {
		form: "space-y-2"
	},
	formInputs: {
		username: {
			input: {
				type: TextInput,
				isVisible: true,
				placeholder: "Enter username"
			}
		},
		password: {
			input: {
				type: PasswordInput,
				isVisible: true,
				placeholder: "Enter password"
			}
		}
	},
	formButton: {
		name: "Sign Up",
		style: "ml-2 py-2 px-3 text-white bg-green-500 rounded-md"
	}
}

component<LoginForm> MyLoginForm {
	globalStyle: {
		form: "space-y-2"
	},
	formInputs: {
		username: {
			input: {
				type: TextInput,
				isVisible: true,
				placeholder: "Enter username"
			}
		},
		password: {
			input: {
				type: PasswordInput,
				isVisible: true,
				placeholder: "Enter password"
			}
		}
	},
	formButton: {
		name: "Login",
		style: "ml-2 py-2 px-3 text-white bg-green-500 rounded-md"
	}
}

component<LogoutButton> MyLogoutButton {
	formButton: {
		name: "Logout"
	}
}

@route("/")
@permissions(IsAuth)
page Tasks {
	render(
		<div className="flex h-screen items-center justify-center bg-slate-800">
			<div className="bg-white rounded shadow p-6 m-4 w-full lg:w-3/4 lg:max-w-lg">
				<div className="mb-4">
					<h1 className="text-grey-darkest text-xl font-bold mb-4">{"Todo List"}</h1>
					<TaskCreateForm />
				</div>
				<TasksComponent />
			</div>
		</div>
	)
}

@route("/login")
page LoginPage {
	render(
		<div className="flex h-screen items-center justify-center bg-slate-800">
			<div className="space-y-2">
				<span className="text-xl text-white">{"Login form"}</span>
				<MyLoginForm/>
			</div>
		</div>
	)
}

@route("/signup")
page SignupPage {
	render(
		<div className="flex h-screen items-center justify-center bg-slate-800">
			<div className="space-y-2">
				<span className="text-xl text-white">{"Sign up form"}</span>
				<MySignupForm/>
			</div>
		</div>
	)
}
